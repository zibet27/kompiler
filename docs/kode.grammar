Program -> TopDeclList .

TopDeclList -> TopDeclList TopDecl
             |
             .

TopDecl -> FunHeader FunBody
         | AlienFunDecl
         | ObjectHeader ObjectBody
         | TypeAlias
         | GlobalVarDecl
         .

FunHeader -> FUN IDENT LBRACE ParamsOpt RBRACE RetTypeOpt .
FunBody   -> SEMICOLON
           | LPAREN ItemListOpt RPAREN SEMICOLON
           .

AlienFunDecl -> ALIEN FUN IDENT LBRACE ParamsOpt RBRACE RetTypeOpt SEMICOLON .

ObjectHeader -> OBJECT IDENT .
ObjectBody   -> SEMICOLON
              | LPAREN FieldDeclsOpt RPAREN SEMICOLON
              .

TypeAlias -> TYPEALIAS IDENT ASSIGN Type SEMICOLON .

GlobalVarDecl -> Type DeclaratorList SEMICOLON .

ParamsOpt -> Params
           |
           .
Params    -> ParamList .
ParamList -> ParamList COMMA Param
           | Param
           .
Param -> IDENT COLON Type .

RetTypeOpt -> COLON Type
            |
            .

FieldDeclsOpt -> FieldDecls
               |
               .
FieldDecls    -> FieldDecls FieldDecl
               | FieldDecl
               .
FieldDecl -> IDENT COLON Type SEMICOLON .

DeclaratorList -> DeclaratorList COMMA Declarator
                | Declarator
                .

Declarator -> IDENT ArrayDimensionsOpt InitOpt .
ArrayDimensionsOpt -> ArrayDimensions
                    |
                    .
ArrayDimensions    -> ArrayDimensions LBRACKET Expr RBRACKET
                    | LBRACKET Expr RBRACKET
                    .

InitOpt -> WITH Expr
         | ASSIGN Expr
         |
         .

Type -> NonFuncType
      | LBRACE ParamTypeListOpt RBRACE ARROW Type
      .

NonFuncType -> PrimaryType
             | NonFuncType PTR
             .

PrimaryType -> BaseType
             | TYPENAME
             | LPAREN Type RPAREN
             .

BaseType -> VOID | I32 | U8 | F64 .

ParamTypeListOpt -> ParamTypeList
                  |
                  .
ParamTypeList -> ParamTypeList COMMA Type
               | Type
               .

ItemListOpt -> ItemList
             |
             .
ItemList -> ItemList Item
          | Item
          .

Item -> LocalVarDecl SEMICOLON
      | Expr SEMICOLON
      | SKIP SEMICOLON
      | STOP SEMICOLON
      .

LocalVarDecl -> Type DeclaratorList .

Expr -> AssignExpr .

AssignExpr -> UnaryExpr AssignOp AssignExpr
            | LogicalOrExpr
            .
AssignOp -> ASSIGN .

LogicalOrExpr -> LogicalOrExpr OR_OR LogicalAndExpr
               | LogicalAndExpr
               .

LogicalAndExpr -> LogicalAndExpr AND_AND BitwiseOrExpr
                | BitwiseOrExpr
                .

BitwiseOrExpr -> BitwiseOrExpr PIPE BitwiseXorExpr
               | BitwiseXorExpr
               .

BitwiseXorExpr -> BitwiseXorExpr CARET BitwiseAndExpr
                | BitwiseAndExpr
                .

BitwiseAndExpr -> BitwiseAndExpr AMP EqualityExpr
                | EqualityExpr
                .

EqualityExpr -> EqualityExpr EQ_EQ RelationalExpr
              | EqualityExpr BANG_EQ RelationalExpr
              | RelationalExpr
              .

RelationalExpr -> RelationalExpr LT ShiftExpr
                | RelationalExpr GT ShiftExpr
                | RelationalExpr LTE ShiftExpr
                | RelationalExpr GTE ShiftExpr
                | ShiftExpr
                .

ShiftExpr -> ShiftExpr SHL AdditiveExpr
           | ShiftExpr SHR AdditiveExpr
           | AdditiveExpr
           .

AdditiveExpr -> AdditiveExpr PLUS MultiplicativeExpr
              | AdditiveExpr MINUS MultiplicativeExpr
              | MultiplicativeExpr
              .

MultiplicativeExpr -> MultiplicativeExpr STAR UnaryExpr
                    | MultiplicativeExpr SLASH UnaryExpr
                    | MultiplicativeExpr PERCENT UnaryExpr
                    | UnaryExpr
                    .

UnaryExpr -> PostfixExpr
           | PrefixOp UnaryExpr
           .
PrefixOp -> PLUSPLUS | MINUS_MINUS | AMP | STAR | PLUS | MINUS | BANG | TILDE .

PostfixExpr -> PostfixExpr LBRACKET Expr RBRACKET
             | PostfixExpr LBRACE ArgsOpt RBRACE
             | PostfixExpr DOT IDENT
             | PostfixExpr ARROW IDENT
             | PostfixExpr PLUSPLUS
             | PostfixExpr MINUS_MINUS
             | PostfixExpr TILDE_GT Type
             | PrimaryExpr
             .

ArgsOpt -> ArgList
         |
         .
ArgList -> ArgList COMMA Expr
         | Expr
         .

PrimaryExpr -> IDENT
             | INT
             | FLOAT
             | CHAR_LIT
             | STRING_LIT
             | LPAREN Expr RPAREN
             | BlockExpr
             | IfExpr
             | WhileExpr
             | DoWhileExpr
             | ForExpr
             | SwitchExpr
             .

BlockExpr -> LPAREN ItemListOpt RPAREN .

IfExpr -> IF LBRACE Expr RBRACE BlockExpr ELSE BlockExpr .

WhileExpr -> WHILE LBRACE Expr RBRACE BlockExpr .

DoWhileExpr -> DO BlockExpr WHILE LBRACE Expr RBRACE .

ForExpr -> FOR LBRACE ForInitOpt SEMICOLON ForCondOpt SEMICOLON ForIncrOpt RBRACE BlockExpr .
ForInitOpt -> LocalVarDecl
            | Expr
            |
            .
ForCondOpt -> Expr
            |
            .
ForIncrOpt -> Expr
            |
            .

SwitchExpr -> SWITCH LPAREN Expr RPAREN LBRACE CaseListOpt DefaultCaseOpt RBRACE .
CaseListOpt -> CaseList
             |
             .
CaseList -> CaseList Case
          | Case
          .
Case -> INT ARROW Expr SEMICOLON .
DefaultCaseOpt -> ELSE ARROW Expr SEMICOLON
                |
                .